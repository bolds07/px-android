apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}


task jacocoTestReport(type:JacocoReport, dependsOn:['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*',
                      '**/model/*.*',
                      '**/model/**',
                      '**/models/*.*',
                      '**/models/**',
                      '**/viewmodel/*.*',
                      '**/viewmodel/**']

    def debugTree = fileTree(dir:"$project.buildDir/intermediates/classes/debug", excludes:fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir:project.buildDir, includes:[
        'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

/**
 * When building on travis and running connectedDebugAndroidTest task
 * sometimes trigger Execution failed for task ':sdk:connectedDebugAndroidTest'.
 * When certain test take too long to finish.
 * TODO fix the test that generates the delay checking reports
 */
project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}